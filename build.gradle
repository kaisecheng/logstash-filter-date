/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: "java"
apply plugin: "distribution"
apply plugin: "idea"

// TODO(sissel): Move this to a file shared by the gemspec.
group "org.logstash.filters"
version "3.1.6"

import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath group: 'org.jruby', name: 'jruby-complete', version: "9.2.11.0"
  }
}
java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly group: "org.apache.logging.log4j", name: "log4j-api", version: "2.17.1"
  // Keep Joda version same as JRuby 9.2.20.1
  compileOnly group: "joda-time", name: "joda-time", version: "2.10.5"
  compileOnly group: 'org.jruby', name: 'jruby-complete', version: "9.2.11.0"
  compileOnly fileTree(dir: logstashCoreGemPath, include: '**/*.jar')

  testImplementation group: 'junit', name: 'junit', version: '4.12'
  testImplementation group: "org.apache.logging.log4j", name: "log4j-core", version: "2.17.1"
  testImplementation group: "joda-time", name: "joda-time", version: "2.10.5"
  testImplementation group: 'org.jruby', name: 'jruby-complete', version: "9.2.11.0"
  testImplementation fileTree(dir: logstashCoreGemPath, include: '**/*.jar')
}

task rubyBootstrap {
  doLast {
    description "Try bundler"
    def jruby = new org.jruby.embed.ScriptingContainer()
    def paths = jruby.getLoadPaths()
    paths.add("${projectDir}/lib".getStrings()[0])
    jruby.setLoadPaths(paths)
    jruby.getEnvironment().put("GEM_HOME", "${buildDir}/ruby".toString())
    jruby.getEnvironment().put("GEM_SPEC_CACHE", "${buildDir}/cache".toString())
    jruby.getEnvironment().put("GEM_PATH", "${buildDir}/ruby".toString())
    jruby.runScriptlet(org.jruby.embed.PathType.ABSOLUTE, "${projectDir}/buildSrc/src/main/ruby/bootstrap.rb")
    jruby.runScriptlet("bundler()")
  }
}

task rubyTest(dependsOn: [rubyBootstrap, build]) {
  doLast {
    def jruby = new org.jruby.embed.ScriptingContainer()
    def paths = jruby.getLoadPaths()
    paths.add("${projectDir}/lib".getStrings()[0])
    jruby.setLoadPaths(paths)
    jruby.getEnvironment().put("GEM_HOME", "${buildDir}/ruby".toString())
    jruby.getEnvironment().put("GEM_SPEC_CACHE", "${buildDir}/cache".toString())
    jruby.getEnvironment().put("GEM_PATH", "${buildDir}/ruby".toString())
    jruby.runScriptlet('require "jars"');
    jruby.runScriptlet('require "./build/libs/logstash-filter-date.jar"');
    jruby.runScriptlet("require 'rspec/core/runner'");
    jruby.runScriptlet("require 'rspec'");
    jruby.runScriptlet("RSpec::Core::Runner.run(['spec', '--fail-fast', '--order=rand:1'])");
  }
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file("lib/${project.name}_jars.rb")
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
                def id = artifact.id.componentIdentifier
                if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                    w << "require_jar(\'${id.group}\', \'${id.module}\', \'${id.version}\')\n"
                }
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
            def id = artifact.id.componentIdentifier
            if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                String artifactGroup = id.group
                String artifactName = id.module
                String artifactVersion = id.version
                String groupPath = artifactGroup.replaceAll('\\.', '/')
                File newJarFile = file("${vendorPathPrefix}/${groupPath}/${artifactName}/${artifactVersion}/${artifactName}-${artifactVersion}.jar")
                if (!newJarFile.parentFile.exists()) {
                    newJarFile.parentFile.mkdirs()
                }
                Files.copy(artifact.file.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
            }
        }
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)
